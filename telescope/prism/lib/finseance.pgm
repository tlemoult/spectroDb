finSeance:
	print "-------------------------------------------------"
	print "Fin de seance"
	print "stop entrainment horaire"
	STOPTELESCOPE

	IF (coupole=1) then
		print "fermeture du dome"
		CLOSE_DOME
	ENDIF

	IF (ActiveFlatLISALHIRESFinSession=1) then
		GOSUB acquisition-FLAT-LISA_LHIRES:
		REM on ne fait les flat que une fois par lancement du script..
		ActiveFlatLISALHIRESFinSession=0
	endif

    ///  Telescope ///	
	IF (ActiveParkingTelescope=1) then
		print "parking du telescope"
		
		STR_RAD_TO_RA ParkHa HH$ MM$ SS$
		STR_RAD_TO_DEC ParkDe DD$ DM$ DS$
		PRINT "    Angle horaire = " HH$ "h " MM$ "m " SS$ "s"
   	    PRINT "    Declinaison   = " DD$ "d " DM$ "m" DS$ "s"
		
		GetStdrLongitude Longitude
		NOW date
		SIDERALTIME Date Longitude TSL
		ParkRa=TSL-ParkHa
		STR_RAD_TO_RA ParkRa HH$ MM$ SS$
		PRINT "    Alpha calcule = " HH$ "h " MM$ "m " SS$ "s"

		print "    execution mouvement telescope"
		MoveTelescope  ParkRa ParkDe
		print "    attente fin mouvement telescope"
		WaitForEndMoveTelescope
		print "    arret entrainement"
		STOPTELESCOPE

	ENDIF
	
	print "  recupere position telescope"
	GET_TELESCOPE_POSITION  Alpha2000  Delta2000  Hauteur  Azimuth  AngleHor
	STR_RAD_TO_RA AngleHor HH$ MM$ SS$
	STR_RAD_TO_DEC Delta2000 DD$ DM$ DS$
	PRINT "Etat du telescope fin de seance:"
	PRINT "    Angle horaire = " HH$ "h " MM$ "m " SS$ "s"
	PRINT "    Declinaison   = " DD$ "d " DM$ "m" DS$ "s"
	STR_RAD_TO_DEC Hauteur DD$ DM$ DS$
	print "    Hauteur       = " DD$ "d " DM$ "m" DS$ "s"

	/// Camera ///
	gosub set_cooling_camera_off:

	IF (ActiveCoupureCameraFinSession=1) then
		
		Print "Attente rechauffement camera avant coupure alimentation en millisec=" DurationAttenteAvantCoupeCameraMillis
		delay DurationAttenteAvantCoupeCameraMillis
		WAITFORENDDELAY

		print "Fermeture des panneaux cameras"
//		CLOSE_CCD_CAMERA_EX CameraChamp
//		CLOSE_CCD_CAMERA_EX CameraSpectre
		if (CameraChercheurPresente=1) then
			// commente car l init camera chercheur est bugue dans PRISM V10
			//CLOSE_CCD_CAMERA_EX CameraChercheur
		ENDIF

		Print "Attente avant coupure alim camera"
		delay 5000
		WAITFORENDDELAY
		
    	print "Coupure alimentation camera guidage"
		parm$=PythonScriptControlBox$+" 1=0"
		EXECEXTERNAL   pathPythonInterpreter$ parm$
		
		print "Coupure alimentation camera eShel"
		parm$=PythonScriptControlIPX800$+"  5=0"
		EXECEXTERNAL   pathPythonInterpreter$ parm$

		Print "Attente apres coupure alim camera"
		delay 5000
		WAITFORENDDELAY

		CLEARIMGWINDOWS
		
	ENDIF
	
RETURN

acquisition-FLAT-LISA_LHIRES:

	Print "Realisation des FLAT LISA/LHIRES"
	NomObjet$="FLAT"
	GOSUB setWorkDir:

	Print "   allume lampe FLAT"
	EXECEXTERNAL   pathPythonInterpreter$  PythonScriptFLATLISALHIRESOn$
	Print "   attente apres manipulation lampe " pauseAfterLightOn " secondes"
	delay pauseAfterLightOn*1000
	WaitForEndDelay
	intituleCalib$="flat"
	
	For j=1 NbFlatLISALHIRES
		STR j idCalib$
		Print intituleCalib$ " ,    debut de la pose " j "/" NbFlatLISALHIRES " duree " exposureTimeFLATLISA " secondes"
		StartExposure_EX CameraSpectre (exposureTimeFLATLISA*1000)
		WaitForEndExposure_EX CameraSpectre
		GetLastImageExposure_EX CameraSpectre img1
		
		SaveFIT Img1 WorkDir$+"flat-"+idCalib$
		Close Img1
	Next j
	print "    eteind la lampe FLAT"
	EXECEXTERNAL   pathPythonInterpreter$  PythonScriptFLATLISALHIRESOff$
RETURN